#!/opt/perl/bin/perl

use 5.010;
use warnings;
use strict;
use autodie;

use CPAN::Package;
use Cwd             qw/abs_path/;

$SIG{INT} = sub {
    warn "Interrupt, exiting\n";
    exit 0;
};

my $Conf = CPAN::Package->new(
    builtby     => "ben\@morrow.me.uk",

    perl        => "/opt/perl/bin/perl",
    prefix      => "/opt/perl",
    initpkgs    => [ qw[
        pkg-1.1.3_1
        opt-perl-5.16.3
        mksh-r46
    ]],

    dist        => abs_path("dist"),
    packages    => abs_path("pkg"),
    pkgdb       => abs_path("pkgdb"),

    su          => sub { 
        system "sudo", @_;
    },
);

my $jail    = $Conf->find(Jail => "91R-amd64");
my $pkg     = $jail->pkgtool;
my $pkgdb   = $jail->pkgdb;

$jail->start;
$pkg->install_sys_pkgs($Conf->initpkgs);

my @mods = @ARGV;
my %done;

while (my $mod = shift @mods) {
    my $dist        = $Conf->find(Dist => spec => $mod);
    if ($done{$dist->name}) {
        $Conf->sayf(1, "%s already built, skipping", $dist->name);
        next;
    }
    $dist->fetch;

    my $build       = $Conf->find(Build => $jail, $dist);
    $build->unpack_dist;
    $build->read_meta("META");

    if (my @needed = $build->satisfy_reqs("configure")) {
        unshift @mods, @needed, $mod;
        $Conf->say(1, "Deferring for deps");
        next;
    }
    $build->configure_dist;
    $build->read_meta("MYMETA");

    if (my @needed = $build->satisfy_reqs("build")) {
        unshift @mods, @needed, $mod;
        $Conf->say(1, "Deferring for deps");
        next;
    }
    $build->make_dist($_) for qw/build install/;
    $build->fixup_install;

    $pkg->create_pkg($build);
    $done{$dist->name} = 1;
}

$jail->injail("", $ENV{SHELL});

$jail->stop;
