#!/opt/perl/bin/perl

use 5.010;
use warnings;
use strict;
use autodie;

use CPAN::Package;
use Cwd             qw/abs_path/;

$SIG{INT} = sub {
    warn "Interrupt, exiting\n";
    exit 0;
};

my $Conf = CPAN::Package->new(
    builtby     => "ben\@morrow.me.uk",
    verb        => 2,
    log         => "log",
    redirect_stdh   => 1,

    perl        => "/opt/perl/bin/perl",
    prefix      => "/opt/perl",
    initpkgs    => [ qw[
        pkg-1.1.3_1
        opt-perl-5.16.3
        mksh-r46
    ]],

    dist        => abs_path("dist"),
    packages    => abs_path("pkg"),
    pkgdb       => abs_path("pkgdb"),

    su          => sub { 
        my ($conf, @cmd) = @_;
        $conf->system("sudo", @cmd);
    },
);

my $jail    = $Conf->find(Jail => "91R-amd64");
my $pkg     = $jail->pkgtool;
my $pkgdb   = $jail->pkgdb;

$jail->start;
$pkg->install_sys_pkgs($Conf->initpkgs);

my @mods = @ARGV;
my %tried;

while (my $mod = shift @mods) {
    my $dist        = $Conf->find(Dist => spec => $mod);
    my $distname    = $dist->name;

    if ($tried{$distname}) {
        $Conf->say(1, "Already tried $distname, skipping");
        next;
    }
    $tried{$distname} = 1;
    $dist->fetch;

    my $build       = $Conf->find(Build => $jail, $dist);
    $build->unpack_dist;
    $build->read_meta("META");

    my $check_reqs = sub {
        no warnings "exiting";
        if (my @needed = $build->satisfy_reqs($_[0])) {
            unshift @mods, @needed, $mod;
            delete $tried{$distname};
            $Conf->say(1, "Deferring for deps");
            next;
        }
    };

    $check_reqs->("configure");
    $build->configure_dist;
    $build->read_meta("MYMETA");

    $check_reqs->("build");
    $build->make_dist($_) for qw/build install/;
    $build->fixup_install;

    $pkg->create_pkg($build);
}

$jail->injail("", "sh", "-c", "$ENV{SHELL} >/dev/tty 2>&1");

$jail->stop;
