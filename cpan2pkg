#!/opt/perl/bin/perl

use 5.010;
use warnings;
use strict;
use autodie;

use CPAN::Package;
use Cwd             qw/abs_path/;

$SIG{INT} = sub {
    warn "Interrupt, exiting\n";
    exit 0;
};

my $Conf = CPAN::Package->new(
    builtby     => "ben\@morrow.me.uk",

    perl        => "/opt/perl/bin/perl",
    prefix      => "/opt/perl",
    initpkgs    => [ 
        map "/packages/All/$_.txz", qw[
            pkg-1.1.3_1
            opt-perl-5.16.3
            mksh-r46
    ]],

    dist        => abs_path("dist"),
    packages    => abs_path("pkg"),
    pkgdb       => abs_path("pkgdb"),

    su          => sub { 
        system "sudo", @_;
    },
);

my $jail    = $Conf->find(Jail => "91R-amd64");
my $pkg     = $jail->pkgtool;
my $pkgdb   = $jail->pkgdb;

$jail->start;
$pkg->install_pkgs($Conf->initpkgs);

for my $mod (@ARGV) {
    my $dist        = $Conf->find(Dist => $mod);
    $dist->fetch;

    my $build       = $Conf->build_for($jail, $dist);
    $build->unpack_dist;
    $build->read_meta("META");

    my $req         = $build->needed("configure");
    if ($$req{needed} or $$req{pkg}) {
        say "==> Unsatisfied deps";
        next;
    }

    $build->configure_dist;
    $build->read_meta("MYMETA");

    $req            = $build->needed("build");
    if ($$req{needed} or $$req{pkg}) {
        say "Unsatisfied deps";
        next;
    }

    $build->make_dist($_) for qw/build install/;
    $build->fixup_install;

    $pkg->create_pkg($build);

    my $mods        = $build->provides;
    $pkgdb->register_dist($dist, $mods);
}

$jail->injail("", $ENV{SHELL});

$jail->stop;
